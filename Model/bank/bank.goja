model bank {

server Server {
	one-to-one (server-only access) Service service;
}
abstract server Service{
	String changePassword@@Passwort_ändern(String newPassword1 PASSWORD, String newPassword2 PASSWORD) throws PasswordException;
}
server Administrator extends Service {
	observe CurrencyManager currencyManager;
	observe Bank ** banks;
	void createBank@@Bank_erstellen(String name);
	void changeName@@Namen_ändern(Bank bank, String name@@Name);
	void changeCurrencyRateGUI@@Wechselkurs_festlegen(CurrencySUBTYPEName currency, Fraction rate);
	Bank searchBankByBankNumber(Integer bankNum) throws InvalidBankNumberException;
}

singleton CurrencyManager@@Zentralbank {
	Amount** Currency** exchangeRates;
	Money** currencyStock;
	Money translateMoneyWithoutChanging(Money money, Currency target);
	Money translateMoney(Money money, Currency target);
	Amount translateMoneyToReferenceWithoutChanging(Money money);
	Amount calculateExchangeRateCompensationReferenceAmount();
	void shiftCurrencyStock(Money variance);

	void changeExchangeRate(Currency currency, Amount newRate);
	Amount fetchExchangeRate(Currency currency);
}

singleton BankCreator {
	server-only Integer lastBankNumber;
	active Bank createBank(String name, Administrator administrator);
}
server BankService extends Service {
	skip-view prior final one-to-one (server-only bankServices) Bank bank;
	void createAccount@@Neues_Konto(CurrencySUBTYPEName currencyType@@Währung);
	void closeAccount@@Konto_schließen(Account acc) throws CloseAccountNoPossibleException;
	void closeAccount@@Konto_übertragen(Account acc, Account transAcc{bank;currentAccounts}) throws ExecuteException, CloseAccountNoPossibleException;
	void changeMaxLimit@@Obergrenze_festlegen(LimitAccount limit,Fraction amount@@Obergrenze);
	void changeMinLimit@@Untergrenze_festlegen(LimitAccount limit, Fraction amount@@Untergrenze);
	void resetMaxLimit@@Obergrenze_aufheben(LimitAccount limit);
	void resetMinLimit@@Untergrenze_aufheben(LimitAccount limit);
	void findAccount@@Konto_finden(Integer accountNumber) throws UserException;
	void changeTransactionFee@@Gebühren_ändern(TransactionFeeSUBTYPEName newFee@@Gebührentyp, Fraction fix@@fixe_Gebühren, CurrencySUBTYPEName fixCurrency, Fraction limit@@Grenze, CurrencySUBTYPEName limitCurrency, Fraction procentual@@variable_Gebühren);
}
class Bank {
	indexed Integer bankNumber@@BLZ;
	indexed prior String name@@Name;
	server-only Integer lastAccountNumber##lstAccNo;
	TransactionFee fee;
	InternalFee internalFee;
	prior Account ownAccount;
	server-only one-to-one (server-only bank) Account ** Integer ** accounts;
	symmetric (server-only banks) prior server-only Administrator administrator;
	transient Account ** currentAccounts;
	active event void changeName(String name);
	active void createAccount(CurrencySUBTYPEName currencyType);
	void sendTransfer(DebitTransfer debitTransfer) throws InvalidBankNumberException, InvalidAccountNumberException, DebitException;
	void receiveTransfer(DebitTransfer debitTransfer) throws InvalidAccountNumberException, DebitException;
	void changeTransactionFeeToFix(Money fix);
	void changeTransactionFeeToProcentual(Percent procentual);
	void changeTransactionFeeToMixed(Money fix, Percent procentual, Money limit);
	Account searchAccountByAccNumber(Integer accNum) throws InvalidAccountNumberException;
}
server AccountService extends Service {
	prior final one-to-one (server-only  accountService) skip-view Account account;
	DebitTransferSuccessful successful;
	DebitTransferNotExecuted notExecuted;
	DebitTransferTemplate template;
	void createTransfer@@Neue_Überweisung();

	void createDebit@@Neue_Lastschrift();
	void createTemplate@@Neue_Vorlage(DebitTransferTransactionSUBTYPEName type);
	void createTransaction@@Neue_Transaktion();
	void executeTransfer@@Überweisung_abschicken(DebitTransferTransaction debitTransfer) throws ExecuteException, NoPermissionToExecuteDebitTransferException;
	active void useTemplate@@Vorlage_verwenden(Transfer transfer);

	void changeReceiverBank@@Empfänger_Bank_ändern(DebitTransfer trans, Integer receiverBankNumber);
	void changeReceiverAccount@@Empfänger_Konto_ändern(DebitTransfer trans, Integer receiverAccNumber);
	void changeMoney@@Überweisungsbetrag_ändern(DebitTransfer trans, Fraction newAmount);
	void changeSubject@@Betreff_ändern(DebitTransfer trans, String subject);
	void changeCurrency@@Währung_ändern(DebitTransfer trans, CurrencySUBTYPEName currency);
	active void createDebitGrant@@Neue_Erlaubnis_erteilen(DebitGrantListe debitGrantList,Integer receiverBankNumber,Integer receiverAccNumber, LimitTypeSUBTYPEName limitType, Fraction amount, CurrencySUBTYPEName cur) throws InvalidAccountNumberException, InvalidBankNumberException;
	void addToTransaction(Transaction transaction,DebitTransfer debitTransfer);
}

class DebitTransferSuccessful##DbtTrnSucc {
	DebitTransferTransaction ** successfuls;
}

class DebitTransferNotExecuted##DbTrNtExec {
	DebitTransferTransaction ** notExecuteds;	
}

class DebitTransferTemplate##DbTrNtTmpl {
	DebitTransferTransaction ** templates;	
}

class Account {
	prior indexed Integer accountNumber@@Kontonummer;
	prior one-to-one (no-view account) Money money@@Geldmenge;
	one-to-one (no-view account) LimitAccount limit;
	no-view DebitTransferTransaction ** debitTransferTransactions##DebTrfTrans;
	observe DebitGrantListe grantedDebitGrant##GrntdGrnt;
	observe DebitGrantListe receivedDebitGrant##RcvdGrnt;
 
	Transfer createTransfer();
	void changeReceiverBank(DebitTransfer trans, Integer receiverBankNumber);
	void changeReceiverAccount(DebitTransfer trans, Integer receiverAccountNumber);
	void changeMoney(DebitTransfer trans, Fraction newAmount);
	void changeCurrency(DebitTransfer trans, Currency currency);

	Debit createDebit();
	event void createDebitGrant(Account receiver, LimitType limit);

	DebitTransferTransaction createTemplate(DebitTransferTransactionSUBTYPEName type);
	Transaction createTransaction();
}

class AccountPx {
	server-only prior Account account;
}

class DebitGrantListe {
	DebitGrant ** debitGrants;
	event void createDebitGrant(AccountPx receiver, LimitType limit);
}

class Money {
	skip-view prior Amount amount;
	skip-view prior Currency currency;
	Money add(Money money);
	Money subtract(Money money) throws LimitViolatedException;
	Money multiply (Fraction factor);
	BooleanValue greater(Money money);
	BooleanValue greaterOrEqual(Money money);
	BooleanValue equalsValue(Money money);
}

class Amount {
	prior Fraction balance;
	Amount add(Amount a);
	Amount subtract(Amount a);
}

class LimitAccount {
	no-view LimitType minLimit;
	no-view LimitType maxLimit;
	void checkLimit(Money money) throws LimitViolatedException;
}

abstract class BooleanValue(TrueValue,FalseValue){
	extern boolean isTrue();
}

abstract string-factory class LimitType(NoLimit) {
	void checkLimit(Money money) throws LimitViolatedException;
}
class Limit extends LimitType{
	prior skip-view Money money;
}

string-factory abstract class TransactionFee{}

class MixedFee extends TransactionFee{
	prior FixTransactionFee fix;
	prior ProcentualFee procentual;
	prior Money limit;
}
class ProcentualFee extends TransactionFee{
	prior no-view Percent percent;
}
class FixTransactionFee extends TransactionFee {
	prior no-view Money value;
}

class InternalFee {
 	prior no-view Percent percent;
}
class Percent {
	prior Fraction value;
}

string-factory abstract class Currency (Euro, Dollar, Yen, Pfund, Franken){}

string-factory abstract class DebitTransferTransaction##DebiTrfTran {
	active event void execute()  throws ExecuteException;
	Timestamp timestamp@@Zeitstempel;
	no-view	Account sender;
	one-to-one (no-view debitTransfer) DebitTransferState state;
}

abstract class DebitTransfer extends DebitTransferTransaction{
	Integer receiverAccountNumber##recaccno@@Empfänger_Konto;
	Integer receiverBankNumber@@Empfänger_Bank;	
	no-view Money money@@Betrag;
	String subject@@Betreff;
	no-view one-to-one (no-view debitTransfer) StornoState stornoState;
	abstract Money fetchRealMoney();
}

class Transfer extends DebitTransfer {
	Transfer copy();
}
class Debit extends DebitTransfer {}

class Transaction extends DebitTransferTransaction {
	skip-view DebitTransferListe debitTransfer;
	void addToTransaction(DebitTransfer debitTransfer);
}

class DebitTransferListe##DebTransLst {
	DebitTransfer ** debitTransfers;
}

class DebitGrant {
	prior AccountPx permittedAccount;
	prior LimitType limits;
}

abstract class DebitTransferState##DebiTraStat(SuccessfulState,NotSuccessfulState##NotSucState,TemplateState,ExecutedState,NotExecutedState,NotExecutableState){
	BooleanValue isExecutable();
	void changeState(DebitTransferState newState);
	void removeDebitTransferFromList();
	void addDebitTransferFromList(DebitTransferState state);
}

abstract class StornoState(SuccessfulStornoState##SucStoSta,NotSuccessfulStornoState##NoSucStoSta,RequestState,NoRequestState){}

exception PasswordException {}
exception InvalidBankNumberException extends ExecuteException{}
exception InvalidAccountNumberException extends ExecuteException{}
exception LimitViolatedException extends DebitException{}
exception NoPermissionToExecuteDebitTransferException extends ExecuteException{}
exception CloseAccountNoPossibleException{}
exception NoAccountsFound{}
exception DebitNotGrantedException extends DebitException{}

abstract exception DebitException extends ExecuteException{}
abstract exception ExecuteException{}
}


